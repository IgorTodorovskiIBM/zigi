/* --------------------  rexx procedure  -------------------- *
 | Name:      zigimrgm                                        |
 |                                                            |
 | Function:  ISPF Edit Macro to:                             |
 |                                                            |
 |   Parm M: remove all non-merge records                     |
 |   Parm blank: remove all merge records                     |
 |               and compare merge-file x                     |
 |                                                            |
 | Usage Notes: called by zigi                                |
 |                                                            |
 | Author:    Lionel B. Dyck                                  |
 |                                                            |
 | History:  (most recent on top)                             |
 |            12/08/19 LBD - creation                         |
 |                                                            |
 * ---------------------------------------------------------- */
  Address ISREdit
  'Macro (options)'
  'reset all'
  'recovery on'
  address ispexec 'vget (mdsn mrgdir mrgfile)'

  if options = '' then do
    '(last) = linenum .zlast'
    hit = 0
    do i = last to 1 by -1
      '(data) = line' i
      Select
      when pos('<<<<<<<',data) > 0 then do
        'delete' i
        hit = 0
      end
      when pos('=======',data) > 0 then do
        'delete' i
        hit = 0
      end
      when pos('>>>>>>>',data) > 0 then do
        'delete' i
        hit = 1
      end
      when hit = 1 then do
        'delete' i
      end
      otherwise nop
    end
    end
    'Compare' mrgfile 'x'
  end

  if options = 'M' then do
    '(last) = linenum .zlast'
    hit = 0
    do i = last to 1 by -1
      '(data) = line' i
      Select
      when pos('<<<<<<<',data) > 0 then do
        'delete' i
        hit = 0
      end
      when pos('=======',data) > 0 then do
        'delete' i
        hit = 1
      end
      when pos('>>>>>>>',data) > 0 then do
        'delete' i
        hit = 0
      end
      when hit = 1 then do
        'delete' i
      end
      otherwise nop
    end
    end
    'End'
    exit
  end

  'Cursor = 1'
  exit 0
